/*
 * Copyright 2020 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/jacoco.gradle"

project.ext.limits = [
        'instruction': 70,
        'branch'     : 53,
        'line'       : 75,
        'complexity' : 60,
        'method'     : 65,
        'class'      : 83
]

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    api 'com.epam.reportportal:client-java:5.2.1'
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'com.epam.reportportal:commons-model:5.0.0'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.testng:testng:7.9.0'
    implementation 'org.slf4j:slf4j-api:2.0.4'

    testImplementation 'com.epam.reportportal:agent-java-test-utils:0.0.2'

    testImplementation("org.junit.platform:junit-platform-runner:${junit5_runner_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"

    testImplementation 'org.aspectj:aspectjweaver:1.9.19'

    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.4.12'

    testImplementation 'com.epam.reportportal:logger-java-logback:5.2.0'

    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'com.ibm.icu:icu4j:67.1'
    testImplementation ('com.google.inject:guice:5.1.0') {
        exclude module: 'guava'
    }
    testImplementation 'com.google.guava:guava:33.0.0-jre'
}

test {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    exclude('com/epam/reportportal/testng/integration/feature/**')
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains('aspectjweaver') }
        jvmArgs += "-javaagent:$weaver"
    }
    environment 'AGENT_NO_ANALYTICS', '1'
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

build.dependsOn jacocoTestReport
